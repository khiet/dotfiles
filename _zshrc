# https://github.com/robbyrussell/oh-my-zsh/blob/master/templates/zshrc.zsh-template
export ZSH=$HOME/.oh-my-zsh

# brew
eval "$(/opt/homebrew/bin/brew shellenv)"

CASE_SENSITIVE="true"
DISABLE_UPDATE_PROMPT="true"

# https://github.com/robbyrussell/oh-my-zsh/issues/3466
HIST_STAMPS="%d/%m/%y %T "
alias hs='fc -il 1'
# https://www.soberkoder.com/better-zsh-history/
export HISTFILESIZE=20000 # history size during a session
export HISTSIZE=20000 # history size at start of a session
export SAVEHIST=20000 # history size in a file
# export HISTFILE=~/.zsh_history
setopt HIST_FIND_NO_DUPS
setopt INC_APPEND_HISTORY

# standard plugins can be found in ~/.oh-my-zsh/plugins/*
plugins=(
  colored-man-pages
  urltools
  zsh-syntax-highlighting
)

ZSH_DISABLE_COMPFIX="true"
source $ZSH/oh-my-zsh.sh

# based on https://github.com/ohmyzsh/ohmyzsh/blob/master/themes/robbyrussell.zsh-theme
PROMPT="%(?:%{$fg_bold[green]%}➜ :%{$fg_bold[red]%}➜ )"
PROMPT+='%{$fg[cyan]%}%c%{$reset_color%} $(git_prompt_info)'

ZSH_THEME_GIT_PROMPT_PREFIX="%{$fg[magenta]%}\uE0A0 "
ZSH_THEME_GIT_PROMPT_SUFFIX="%{$reset_color%} "
ZSH_THEME_GIT_PROMPT_DIRTY="%{$fg[blue]%} %{$fg[yellow]%}✗"
ZSH_THEME_GIT_PROMPT_CLEAN="%{$fg[blue]%}"

export XDG_DATA_HOME=$HOME/.local/share
export XDG_CONFIG_HOME=$HOME/.config
export XDG_CACHE_HOME=$HOME/.cache

export RIPGREP_CONFIG_PATH=$HOME/.ripgreprc

export DEVS_HOME=$HOME/Dropbox/Devs

# vim mode
bindkey -v
export KEYTIMEOUT=1
# https://github.com/robbyrussell/oh-my-zsh/blob/master/plugins/vi-mode/vi-mode.plugin.zsh
bindkey -M vicmd 'v' edit-command-line
bindkey '^h' backward-delete-char

export LESS=-RFX

alias ct='ctags -R .'
alias caly='cal -y'
alias public_ip='curl ipinfo.io/ip'
alias private_ip='ipconfig getifaddr en0'

# tmux
alias ta='tmux attach -t'
alias tls='tmux ls'
alias tkill='tmux kill-session -t'

# ripgrep
alias g="rg --sort-files"
alias G="rg --sort-files --no-ignore"
alias gw="rg --sort-files -w"
function gf { rg --files --sort path | rg --regexp $1 }
function gF { rg --files --no-ignore --sort path | rg --regexp $1 }

# rails
alias be="bundle exec"
alias dc="docker-compose"
alias rr="bin/rails restart"
alias rc="bin/rails console -- --nomultiline"
alias rl="tail -f -n 1000 log/development.log"
alias rlc="tail -f -n 1000 log/development.log | rg -A 6 Controller"
alias rlt="tail -f -n 1000 log/test.log"
alias rltc="tail -f -n 1000 log/test.log | rg -A 6 Controller"

if which bat &>/dev/null; then
  alias cat='bat'
fi

if which exa &>/dev/null; then
  alias ls='exa'
fi

export EDITOR='vim'

# nvim
if which nvim &>/dev/null; then
  alias vi='nvim'
  alias vim=''
  export EDITOR='nvim'
fi

# fzf
if which fzf &>/dev/null; then
  # copied from ~/fzf.zsh generated by $(brew --prefix)/opt/fzf/install (see brew info fzf)
  # auto-completion
  [[ $- == *i* ]] && source "/opt/homebrew/opt/fzf/shell/completion.zsh" 2> /dev/null
  # key bindings
  source "/opt/homebrew/opt/fzf/shell/key-bindings.zsh"

  # --multi to select multiple files with TAB
  export FZF_DEFAULT_OPTS='--no-mouse --height 50% --reverse --multi --info=inline'
  export FZF_DEFAULT_COMMAND='rg --files --hidden'
fi

if which tmuxinator &>/dev/null; then
  alias mux=tmuxinator
  export TMUXINATOR_CONFIG=$DEVS_HOME/tmuxinator
fi

# rbenv
if which rbenv &>/dev/null; then
  eval "$(rbenv init -)"
fi

# nvm
export NVM_DIR="$HOME/.nvm"
[ -s "/opt/homebrew/opt/nvm/nvm.sh" ] && . "/opt/homebrew/opt/nvm/nvm.sh" --no-use
function nvm_use {
  if [[ -f ".nvmrc" && ($(nvm current) = "system")]]; then
    nvm use 1> /dev/null
  fi
}
nvm_use
function chpwd { nvm_use }

# find and replace with rg
function gR  { rg -0 -l "$1" | AGR_FROM="$1" AGR_TO="$2" xargs -0 perl -pi -e 's/$ENV{AGR_FROM}/$ENV{AGR_TO}/g' }
function gfR { rg --files | rg --regexp "$1" | AGR_FROM="$1" AGR_TO="$2" perl -p -e 'print $_; s/$ENV{AGR_FROM}/$ENV{AGR_TO}/' | xargs -n2 mv }
function gfD { rg --files | rg --regexp "$1" | xargs rm }

function mv2 { dirname "$2" | xargs mkdir -p; mv "$1" "$2" }
function cp2 { dirname "$2" | xargs mkdir -p; cp "$1" "$2" }
# list brew dependencies
function brewls { brew list | while read cask; do echo -n $fg[blue] $cask $fg[white]; brew deps $cask | awk '{printf(" %s ", $0)}'; echo ""; done }

function rhs { sed -i '' -e "/$1/d" ~/.zsh_history }

function kebab    { echo  "$1" | sed 's/ /-/g' | sed -r 's/([a-z0-9])([A-Z])/\1-\2/g' | tr "[:upper:]" "[:lower:]" | sed -r "s/[,:'‘’()#\"]/-/g" | sed "s|[][-]|-|g" }
function sentence { echo  "$1" | sed 's/-/ /g' | awk -vFS="" -vOFS="" '{$1=toupper($1);print $0}' }
function sp       { sentence "$1" | pbcopy }

# git
alias gdel="git branch --merged | egrep -v '(^\*|master)' | xargs git branch -d"
function gbr { git co -b $(kebab "$1") }
function gsp { sp $(git rev-parse --abbrev-ref HEAD) }

# youtube-dl
function dl {
  if [[ $1 && $2 ]]; then
    youtube-dl -f 'bestvideo[ext=mp4]+bestaudio[ext=m4a]/best' -o "~/youtube/$2.%(ext)s" $1
  elif [[ $1 ]]; then
    youtube-dl -f 'bestvideo[ext=mp4]+bestaudio[ext=m4a]/best' -o '~/youtube/%(title)s-%(id)s.%(ext)s' $1
  fi
}

# postgres
export PATH="/opt/homebrew/opt/postgresql@13/bin:$PATH"

function db_restore { pg_restore --verbose --clean --no-acl --no-owner -h localhost -d portify-api_development $DEVS_HOME/dumps/$1 }

function db_dump {
  if [[ $1 ]]; then
    pg_dump -Fc --no-acl --no-owner -h localhost portify-api_development > $DEVS_HOME/dumps/$(kebab $(date +%b_%d_%a_%H%M)-$(kebab $1)).dump
  else
    pg_dump -Fc --no-acl --no-owner -h localhost portify-api_development > $DEVS_HOME/dumps/$(kebab $(date +%b_%d_%a_%H%M)).dump
  fi
}
